/*
 * Copyright 2020 Wooga GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'signing'
    id 'nebula.release' version '15.2.0'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'io.codearte.nexus-staging' version '0.30.0'
    id "de.marcphilipp.nexus-publish" version "0.4.0"
}

group "com.wooga.xcodebuild"

List<String> cliTasks = project.rootProject.gradle.startParameter.taskNames
if (cliTasks.contains("rc")) {
    cliTasks.remove("rc")
    cliTasks.add("candidate")
    project.rootProject.gradle.startParameter.setTaskNames(cliTasks)
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.4.15'
    implementation 'org.apache.commons:commons-io:[1.3.2,2)'
    testImplementation 'org.spockframework:spock-core:1.2-groovy-2.4'
    testImplementation 'com.github.stefanbirkner:system-rules:[1.18,2)'
    testImplementation 'net.bytebuddy:byte-buddy:[1.10.19,2)'
}

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        main(MavenPublication) {
            artifactId = project.name
            from(components["java"])
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            pom {
                name = 'xcpretty'
                description = 'A fast and flexible formatter for xcodebuild.'
                url = 'https://github.com/wooga/xcpretty.groovy'

                artifactId = project.name
                inceptionYear = "2019"

                scm {
                    connection = 'scm:git:https://github.com/wooga/xcpretty.groovy.git'
                    developerConnection = 'scm:git:https://github.com/wooga/xcpretty.groovy.git'
                    url = 'https://github.com/wooga/xcpretty.groovy.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'manfred.endres'
                        name = 'Manfred Endres'
                        email = 'manfred.endres@wooga.net'
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

nexusStaging {
    username = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : System.getenv('OSSRH_USERNAME')
    password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : System.getenv('OSSRH_PASSWORD')
    packageGroup = "com.wooga"
}

signing {
    def signingKeyId = project.hasProperty("signingKeyId") ? project.property('signingKeyId') : System.getenv('OSSRH_SIGNING_KEY_ID')
    def signingKey = project.hasProperty("signingKey") ? project.property('signingKey') : System.getenv('OSSRH_SIGNING_KEY')
    def signingPassword = project.hasProperty('signingPassphrase') ? project.property('signingPassphrase') : System.getenv("OSSRH_SIGNING_PASSPHRASE")
    useInMemoryPgpKeys(signingKeyId.toString(), signingKey.toString(), signingPassword.toString())
    sign publishing.publications.main
}

postRelease.dependsOn(tasks.publish)
tasks."final".dependsOn(tasks.closeAndReleaseRepository)
tasks."candidate".dependsOn(tasks.closeAndReleaseRepository)
tasks.closeAndReleaseRepository.mustRunAfter(tasks.postRelease)
tasks.publish.mustRunAfter(tasks.release)
